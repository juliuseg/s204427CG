<!DOCTYPE html>
<html>
<head>
  <script id="vertex-shader-teapot" type="x-shader/x-vertex">
    attribute vec4 vPosition;      // Vertex position
    attribute vec3 vNormal;       // Vertex normal

    uniform mat4 mvpMatrix;       // Model-View-Projection matrix
    uniform mat4 modelMatrix;     // Model matrix for transforming normals
    uniform vec3 lightPosition;   // Position of the point light in world space

    uniform mat4 uLightMVP;        // Light's Model-View-Projection (MVP) matrix
    varying vec4 vLightSpacePos;   // Pass light-space position to the fragment shader


    varying float lightIntensity; // Output light intensity to the fragment shader

    void main() {
        // Compute the position in world space
        vec3 worldPosition = vec3(modelMatrix * vPosition);

        // Compute the normal in world space
        vec3 transformedNormal = normalize(mat3(modelMatrix) * vNormal);

        // Compute the light direction (from world position to light)
        vec3 lightDirection = normalize(lightPosition - worldPosition);

        // Calculate diffuse light intensity (Lambertian reflection)
        lightIntensity = max(dot(transformedNormal, lightDirection), 0.0);

        // Transform the vertex position to light space
        vLightSpacePos = uLightMVP * vPosition;

        // Pass the transformed position to the pipeline
        gl_Position = mvpMatrix * vPosition;

        

    }
  </script>

  <script id="fragment-shader-teapot" type="x-shader/x-fragment">
    precision mediump float;

    varying float lightIntensity; // Light intensity from the vertex shader
    varying vec4 vLightSpacePos;  // Interpolated light-space position

    uniform sampler2D uShadowMap; // Shadow map texture

    void main() {
        // Base color of the teapot
        vec4 baseColor = vec4(0.7, 0.0, 0.0, 1.0); // Red

        // Transform light-space position to NDC
        vec3 lightNDC = vLightSpacePos.xyz / vLightSpacePos.w;

        // Map NDC [-1, 1] to [0, 1] for shadow map texture coordinates
        vec2 shadowTexCoord = lightNDC.xy * 0.5 + 0.5;

        // Fetch the depth from the shadow map
        float shadowMapDepth = texture2D(uShadowMap, shadowTexCoord).r;

        // Use the same calculation for the current depth as in the shadow map shader
        float currentDepth = lightNDC.z * 0.5 + 0.5;

        // Add a small bias to avoid shadow acne
        float bias = 0.005;
        float shadowFactor =  currentDepth > shadowMapDepth + bias ? 0.5: 1.0;

        // Apply the shadow factor to the base color, modulated by light intensity
        gl_FragColor = vec4(baseColor.rgb * shadowFactor * lightIntensity, baseColor.a);
    }
  </script>



  <script id="vertex-shader-ground" type="x-shader/x-vertex">
    attribute vec4 vPosition;       // Vertex position
    attribute vec2 vTexCoord;      // Texture coordinates

    uniform mat4 mvpMatrix;        // Camera's Model-View-Projection (MVP) matrix
    uniform mat4 uLightMVP;        // Light's Model-View-Projection (MVP) matrix

    varying vec2 fTexCoord;        // Pass texture coordinates to the fragment shader
    varying vec4 vLightSpacePos;   // Pass light-space position to the fragment shader

    void main() {
        // Transform the vertex position to camera space
        gl_Position = mvpMatrix * vPosition;

        // Transform the vertex position to light space
        vLightSpacePos = uLightMVP * vPosition;

        // Pass texture coordinates to the fragment shader
        fTexCoord = vTexCoord;
    }
  </script>


  <script id="fragment-shader-ground" type="x-shader/x-fragment">
    precision mediump float;

    varying vec2 fTexCoord;        // Interpolated texture coordinates
    varying vec4 vLightSpacePos;   // Interpolated light-space position

    uniform sampler2D texture;    // Ground texture sampler
    uniform sampler2D uShadowMap; // Shadow map texture

    void main() {
        // Sample the ground texture
        vec4 baseColor = texture2D(texture, fTexCoord);

        // Transform light-space position to clip space
        vec3 lightNDC = vLightSpacePos.xyz / vLightSpacePos.w;

        // Map NDC [-1, 1] to [0, 1] for shadow map texture coordinates
        vec2 shadowTexCoord = lightNDC.xy * 0.5 + 0.5;

        // Fetch the depth from the shadow map
        float shadowMapDepth = texture2D(uShadowMap, shadowTexCoord).r;

        // Compute current depth using the same formula as the shadow map generation
        float currentDepth = lightNDC.z * 0.5 + 0.5;

        // Add a small bias to avoid shadow acne
        float bias = 0.005;
        float shadowFactor =  currentDepth > shadowMapDepth + bias ? 0.5: 1.0;
        

        // Apply the shadow factor to the base color
        gl_FragColor = vec4(baseColor.rgb * shadowFactor, baseColor.a*0.7);
    }
  </script>


  <script id="vertex-shader-shadow" type="x-shader/x-vertex">
    attribute vec4 vPosition;

    uniform mat4 mvpMatrix;

    
    void main() {
        // Compute clip-space position
        gl_Position = mvpMatrix * vPosition;
    }
  </script>


  <script id="fragment-shader-shadow" type="x-shader/x-fragment">
    precision mediump float;


    void main() {
        // Use the depth value to set the color (darker for further objects)
        gl_FragColor = vec4(vec3(gl_FragCoord.z), 1.0);
    }
  </script>



  <script type="text/javascript" src="../../Common/webgl-utils.js"></script>
  <script type="text/javascript" src="../../Common/initShaders.js"></script>
  <script type="text/javascript" src="../../Common/MV.js"></script>
  <script type="text/javascript" src="part1.js"></script>
  <script type="text/javascript" src="../res/OBJParser.js"></script>
  <script type="text/javascript" src="../res/Camera.js"></script>

</head>
<body>
  <canvas id="gl-canvas" width="512" height="512">
    Sorry; your web browser does not support HTML5â€™s canvas element.
  </canvas>
</body>
</html>